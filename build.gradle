plugins {
	id 'application'
	id 'eclipse'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'jakarta.mail:jakarta.mail-api:2.1.2'
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	implementation 'org.apache.commons:commons-text:1.11.0'
	implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
	implementation 'org.apache.poi:poi-ooxml:5.2.5'
}

group = 'org.virginiaso'
version = '0.0.3'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all'
}

application {
	mainClass = 'org.virginiaso.roster_cruncher.App'
}

testing {
	suites {
		test {
			useJUnitJupiter('5.9.3')
		}
	}
}

task getPortalRoster(type: JavaExec) {
	dependsOn 'classes'
	mainClass = 'org.virginiaso.roster_cruncher.PortalRetriever'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'portal.password', "$portalPassword"
}

task crunchRosters(type: JavaExec) {
	dependsOn 'classes'
	mainClass = 'org.virginiaso.roster_cruncher.App'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'mail.password', "$mailPassword"
}

task sendTestEmail(type: JavaExec) {
	dependsOn 'classes'
	mainClass = 'org.virginiaso.roster_cruncher.Emailer'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'mail.password', "$mailPassword"
}
