plugins {
	id 'application'
	id 'eclipse'
}

group = 'org.virginiaso'
version = '0.0.2'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.apache.commons:commons-csv:1.9.0'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.apache.commons:commons-text:1.9'
	implementation 'com.google.code.gson:gson:2.9.1'
	implementation 'javax.mail:javax.mail-api:1.6.2'
	implementation 'com.sun.mail:javax.mail:1.6.2'
	implementation 'org.apache.poi:poi-ooxml:5.2.2'
	implementation 'org.apache.logging.log4j:log4j-core:2.18.0'	// should match version used by POI

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all'
}

application {
	mainClass = 'org.virginiaso.roster_diff.App'
}

task diffRosters(type: JavaExec) {
	dependsOn 'classes'
	mainClass = 'org.virginiaso.roster_diff.App'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'mail.password', "$mailPassword"
}

task getPortalRoster(type: JavaExec) {
	dependsOn 'classes'
	mainClass = 'org.virginiaso.roster_diff.PortalRetriever'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'portal.password', "$portalPassword"
}

task sendTestEmail(type: JavaExec) {
	dependsOn 'classes'
	mainClass = 'org.virginiaso.roster_diff.Emailer'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'mail.password', "$mailPassword"
}

tasks.named('test') {
	useJUnitPlatform()
}
